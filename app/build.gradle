plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    id "kotlin-kapt"
}

android {
    compileSdk defaultCompileSdkVersion
    buildToolsVersion = defaultBuildToolsVersion

    defaultConfig {
        applicationId "ru.usedesk.sample"
        minSdkVersion defaultMinSdkVersion
        targetSdkVersion defaultTargetSdkVersion
        versionCode = 208
        versionName = project.version
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "${variant.applicationId}_v${variant.versionName}_c${variant.versionCode}.apk"
        }
    }
    compileOptions {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }
    buildFeatures {
        dataBinding = true
    }
    namespace "ru.usedesk.sample"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation project(":common-sdk")
    implementation project(":common-gui")
    implementation project(":chat-sdk")
    implementation project(":chat-gui")
    implementation project(":knowledgebase-sdk")
    implementation project(":knowledgebase-gui")

    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx

    implementation libs.androidx.activity.ktx
    implementation libs.androidx.core.ktx
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.appcompat
    kapt libs.androidx.annotation

    implementation libs.google.material

    implementation libs.glide
    implementation(libs.glide.okhttp3.integration) {
        exclude group: "glide-parent"
    }
    kapt libs.glide.compiler

    implementation libs.google.gson

    implementation libs.androidx.multidex
}
